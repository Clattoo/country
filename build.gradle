plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.9'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.google.protobuf' version '0.9.4'
	id 'com.github.edeandrea.xjc-generation' version '1.6'
}

group = 'guru.qa'
version = '0.0.1-SNAPSHOT'
description = 'country'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/milestone' }
	maven { url 'https://repo.spring.io/snapshot' }
}

dependencyManagement {
	imports {
		mavenBom 'org.springframework.grpc:spring-grpc-dependencies:0.4.0-SNAPSHOT'
	}
}

dependencies {
	implementation('org.springframework.boot:spring-boot-starter-web-services') {
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
	}
	implementation 'org.springframework.grpc:spring-grpc-spring-boot-starter'
	implementation 'io.grpc:grpc-services:1.75.0'


	implementation("io.grpc:grpc-protobuf:1.75.0") {
		exclude group: 'com.google.protobuf'
	}
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.data:spring-data-commons'
	implementation 'org.springframework.boot:spring-boot-starter-graphql'
	implementation 'com.tailrocks.graphql:graphql-datetime-spring-boot-starter:6.0.0'
	implementation "org.flywaydb:flyway-core:11.20.0"
	implementation "org.flywaydb:flyway-database-postgresql:11.20.0"
	annotationProcessor 'org.projectlombok:lombok:1.18.42'
	compileOnly 'org.projectlombok:lombok:1.18.42'
	runtimeOnly 'org.postgresql:postgresql:42.7.8'

	implementation "wsdl4j:wsdl4j:1.6.3"

	xjc "org.glassfish.jaxb:jaxb-xjc:4.0.2"

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

xjcGeneration {
	defaultAdditionalXjcOptions = ['encoding': 'UTF-8']
	schemas {
		wsdlSchema {
			schemaRootDir = "$projectDir/src/main/resources"
			schemaFile = 'country.xsd'
			javaPackageName = 'quru.qa.xml.country'
			sourceSet = 'main'
		}
	}
}

sourceSets {
	main {
		java {
			srcDir('src/main/java')
			srcDir("$buildDir/generated-sources/main/xjc")
		}
	}
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:${dependencyManagement.importedProperties['protobuf-java.version']}"
	}
	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:${dependencyManagement.importedProperties['grpc.version']}"
		}
	}
	generateProtoTasks {
		all()*.plugins {
			grpc {
				option '@generated=omit'
			}
		}
	}
}
